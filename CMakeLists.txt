CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(Sharemind_LibSortNetwork C)

# Version information:
SET(CPACK_PACKAGE_VERSION_MAJOR "1")
SET(CPACK_PACKAGE_VERSION_MINOR "1")
SET(CPACK_PACKAGE_VERSION_PATCH "0")
SET(LIBSORTNETWORK_VERSION
    "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
SET(LIBSORTNETWORK_COMPAT_VERSION "${LIBSORTNETWORK_VERSION}")


# Handle user configuration:
INCLUDE("${CMAKE_CURRENT_SOURCE_DIR}/config.local" OPTIONAL)
INCLUDE("${CMAKE_CURRENT_BINARY_DIR}/config.local" OPTIONAL)


# Shared library:
ADD_COMPILE_OPTIONS(
  "-std=c99" "-Wall" "-Wextra" "-O2" "-pthread"
  "$<$<NOT:$<STREQUAL:$<CONFIGURATION>,Release>>:-ggdb>"
  "$<$<NOT:$<STREQUAL:$<CONFIGURATION>,Release>>:-fno-omit-frame-pointer>"
  "$<$<STREQUAL:$<CONFIGURATION>,Release>:-DNDEBUG>"
  "$<$<STREQUAL:$<CONFIGURATION>,Release>:-fomit-frame-pointer>"
)
ADD_DEFINITIONS(
    "-D_ISOC99_SOURCE=1"
    "-D_POSIX_C_SOURCE=200809L"
    "-D_XOPEN_SOURCE=700"
)
FILE(GLOB_RECURSE LIBSORTNETWORK_SOURCES "${CMAKE_CURRENT_SOURCE_DIR}/src/*.c")
ADD_LIBRARY(libsortnetwork SHARED ${LIBSORTNETWORK_SOURCES})
STRING(REGEX REPLACE "(([^\\.]+\\.[^\\.]+)\\.).*" "\\2" LIBSORTNETWORK_SOVERSION
                     "${LIBSORTNETWORK_VERSION}")
SET_TARGET_PROPERTIES(libsortnetwork PROPERTIES
                      OUTPUT_NAME "sharemind_sortnetwork"
                      VERSION "${LIBSORTNETWORK_VERSION}"
                      SOVERSION "${LIBSORTNETWORK_SOVERSION}")
INSTALL(TARGETS libsortnetwork LIBRARY DESTINATION "lib" COMPONENT "lib")


# Header files:
FILE(GLOB_RECURSE LIBSORTNETWORK_HEADERS "${CMAKE_CURRENT_SOURCE_DIR}/src/*.h")
ADD_CUSTOM_TARGET("include_LIBSORTNETWORK_HEADERS_in_IDE" SOURCES ${LIBSORTNETWORK_HEADERS})
SET(LIBSORTNETWORK_INSTALL_HEADERS
    "${CMAKE_CURRENT_SOURCE_DIR}/src/comparator.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/network.h"
    "${CMAKE_CURRENT_SOURCE_DIR}/src/stage.h"
)
INSTALL(FILES ${LIBSORTNETWORK_INSTALL_HEADERS}
        DESTINATION "include/sharemind/libsortnetwork"
        COMPONENT "dev")


# .in files:
FILE(GLOB_RECURSE LIBSORTNETWORK_INFILES "${CMAKE_CURRENT_SOURCE_DIR}/*.in")
ADD_CUSTOM_TARGET("include_LIBSORTNETWORK_INFILES_in_IDE" SOURCES ${LIBSORTNETWORK_INFILES})
SET(LIBSORTNETWORK_INSTALL_LINK_LIBRARIES
    "-L${CMAKE_INSTALL_PREFIX}/lib"
    "-lsharemind_sortnetwork"
)
SET(LIBSORTNETWORK_INSTALL_INCLUDE_DIRS
    "${CMAKE_INSTALL_PREFIX}/include"
)
SET(LIBSORTNETWORK_INSTALL_DEFINITIONS
    "" # TODO?
)
CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/SharemindLibSortNetworkConfig.cmake.in"
               "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibSortNetworkConfig.cmake" @ONLY)
CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/SharemindLibSortNetworkConfigVersion.cmake.in"
               "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibSortNetworkConfigVersion.cmake"
               @ONLY)
INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibSortNetworkConfig.cmake"
        DESTINATION "lib/SharemindLibSortNetwork"
        COMPONENT "dev")
INSTALL(FILES "${CMAKE_CURRENT_BINARY_DIR}/SharemindLibSortNetworkConfigVersion.cmake"
        DESTINATION "lib/SharemindLibSortNetwork"
        COMPONENT "dev")


# Packaging:
SET(CPACK_PACKAGE_VENDOR "Cybernetica AS")
SET(CPACK_PACKAGE_CONTACT "Sharemind packaging <sharemind-packaging@cyber.ee>")
SET(CPACK_GENERATOR "DEB")
SET(CPACK_DEBIAN_PACKAGE_NAME "libsharemind-sortnetwork")
SET(CPACK_DEBIAN_PACKAGE_RELEASE "cyber1")
SET(CPACK_DEBIAN_COMPRESSION_TYPE "xz")
SET(CPACK_DEB_COMPONENT_INSTALL "ON")
SET(CPACK_DEBIAN_FILE_NAME "DEB-DEFAULT")

SET(CPACK_DEBIAN_LIB_PACKAGE_NAME "libsharemind-sortnetwork")
SET(CPACK_COMPONENT_DEV_DESCRIPTION "Sharemind SortNetwork library development headers")
SET(CPACK_COMPONENT_LIB_DESCRIPTION "Sharemind SortNetwork library")
SET(CPACK_DEBIAN_DEV_PACKAGE_DEPENDS
    "libsharemind-sortnetwork (= ${LIBSORTNETWORK_VERSION}), libc6-dev (>= 2.19)")
SET(CPACK_DEBIAN_LIB_PACKAGE_DEPENDS "libc6 (>= 2.19)")
SET(CPACK_DEBIAN_DEV_PACKAGE_SECTION "libdevel")
SET(CPACK_DEBIAN_LIB_PACKAGE_SECTION "libs")
INCLUDE(CPack)
